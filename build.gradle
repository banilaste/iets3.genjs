// MPS plugin
buildscript {
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    }
    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.4.+'
    }
}

// Git
plugins {
    id 'base'
    id 'maven-publish'
	id 'org.ajoberstar.grgit' version '4.1.0' apply false
}

// Versions
ext.mpsVersion = project.hasProperty("mpsVersion") ? project.property('mpsVersion') : '2020.2'

// Optional properties
def mpsDir = project.hasProperty("mpsDir") ? project.property('mpsDir') : "$buildDir/mps"

configurations {
    mps
	mpsArtifacts
    junitAnt
}

dependencies {
    mps "com.jetbrains:mps:$mpsVersion"
	mpsArtifacts "org.iets3:opensource:$mpsVersion.+"
    junitAnt 'org.apache.ant:ant-junit:1.10.9'
}

repositories {
    mavenLocal()
	maven {
		url 'https://projects.itemis.de/nexus/content/repositories/mbeddr'
	}
    mavenCentral()
}

import org.ajoberstar.grgit.Grgit

// Make BuildLanguages and GenerateLibrariesXml available
import de.itemis.mps.gradle.*

def jdkHome = System.getProperty('java.home')

// Location of the dependencies (if cloned)
def ecmascriptHome = "$buildDir/dependencies/ecmascript4mps"

ext.artifactsDir = file("$rootDir/artifacts")


// Default arguments for ant scripts
def buildScriptClasspath = project.configurations.junitAnt.fileCollection { true } +
	project.files("$jdkHome/lib/tools.jar")

// Download MPS (TODO provide a way to specify external MPS)
task resolveMps(type: Copy) {
    dependsOn configurations.mps
    from {
        configurations.mps.resolve().collect { zipTree(it) }
    }
    into mpsDir
}
resolveMps.onlyIf { !project.hasProperty("mpsDir") }



version = "$mpsVersion-SNAPSHOT"

// Args to the builds scripts, contains both home and artifacts directory 
def defaultScriptArgs = [
	'mps_home'                          : mpsDir,

    // Projects folders
    'ecmascript4mps.home'               : ecmascriptHome,
	'iets3.github.opensource.genjs.home': rootDir,

    // Generated artifacts
	'iets3.github.opensource.artifacts' : "$artifactsDir/org.iets3.opensource",
    'mbeddr.artifacts.platform'         : "$artifactsDir/com.mbeddr.platform",
	'ecmascript4mps.artifacts'  		: "$artifactsDir/ecmascript4mps",

    // Misc
	'build.dir'                         : rootDir,
	'version'                           : version
]

// Declare them as global variable for the MPS plugin (see https://github.com/mbeddr/mps-gradle-plugin)
ext["itemis.mps.gradle.ant.defaultScriptArgs"] = defaultScriptArgs.collect { "-D$it.key=$it.value".toString() }
ext["itemis.mps.gradle.ant.defaultScriptClasspath"] = buildScriptClasspath
ext["itemis.mps.gradle.ant.defaultJavaExecutable"] = file("$jdkHome/bin/java")

task cloneEcmascript {
	doLast {
		try {
			Grgit.open(dir: ecmascriptHome)
			println "iets3.opensource exists, assuming clone to be done"
		} catch(Exception e) {
			def grgit = Grgit.clone(
				dir: ecmascriptHome,
				uri: "https://github.com/mar9000/ecmascript4mps.git",
				refToCheckout: "development",
				checkout: true,
			)
			println grgit.describe()
		}
	}
}

task buildEcmascript(type: BuildLanguages, dependsOn: [cloneEcmascript, resolveMps]) {
	script "$ecmascriptHome/build.xml"
}

task resolveArtifacts(type: Copy, dependsOn: [buildEcmascript]) {
    from {
        configurations.mpsArtifacts.resolve().collect { zipTree(it) }
    }
    into artifactsDir
}

task buildLanguage(type: BuildLanguages, dependsOn: [buildEcmascript, resolveMps, resolveArtifacts]) {
	script "$rootDir/build.xml"
}

task generateLibrariesXml(type: GenerateLibrariesXml) {
    description "Read project libraries from projectlibraries.properties and generate libraries.xml in .mps directory. Libraries are loaded in mps during start."
    defaults rootProject.file('projectlibraries.properties')
	overrides rootProject.file('projectlibraries.overrides.properties')
    destination file('.mps/libraries.xml')
}

task packageLanguages(type: Zip, dependsOn: buildLanguage) {
    description ""
    archiveBaseName = 'org.iets3.opensource.genjs'
    from artifactsDir
    include 'iets3.opensource.genjs/**'
    include 'ecmascript4mps/**'
}

publishing {
    publications {
        genJs(MavenPublication) {
            groupId 'org.iets3.opensource'
            artifactId 'genjs'
            artifact packageLanguages
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.mpsArtifacts.resolvedConfiguration.firstLevelModuleDependencies.each{
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                }
                configurations.mps.resolvedConfiguration.firstLevelModuleDependencies.each{
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.moduleGroup)
                    dependencyNode.appendNode('artifactId', it.moduleName)
                    dependencyNode.appendNode('version', it.moduleVersion)
                    dependencyNode.appendNode('type', it.moduleArtifacts[0].type)
                    dependencyNode.appendNode('scope', 'provided')
                }
            }
        }
    }
}

defaultTasks 'buildLanguage'